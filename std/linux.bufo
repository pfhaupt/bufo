import "prelude.bufo";
import "libc.bufo";
import "./substr.bufo";
import "./string.bufo";
import "./cmd.bufo";
import "./posix/structs.bufo";
import "./posix/functions.bufo";
import "./posix/constants.bufo";

comptime INVALID_HANDLE: Handle = Handle { ptr: 18446744073709551615 };

@os(LINUX) func getLastModified(filePath: &char) -> usize {
    let statbuf: Stat = blank;

    if (posix::stat(filePath, &statbuf) < 0) {
        // NOTE: if output does not exist it 100% must be rebuilt
        if (*errno() == ENOENT) return 0;
        C::fprintf(stderr, "[ERROR] Could not get time of %s: %#010x\n", filePath, posix::strerror(*errno()));
        return 0;
    }
    return statbuf.st_mtime as usize;
}

@os(LINUX) func waitForProcess(proc: Handle) -> bool {
    if (proc.ptr == INVALID_HANDLE.ptr) return false;
    while (true) {
        let wstatus: i32 = 0;
        if (posix::waitpid(proc, &wstatus, 0) < 0) {
            C::fprintf(stderr, "[ERROR] Could not wait on command (pid %d): %s\n", proc, posix::strerror(*errno()));
            return false;
        }

        if (posix::WIFEXITED(wstatus)) {
            let exit_status: i32 = posix::WEXITSTATUS(wstatus);
            if (exit_status != 0) {
                C::fprintf(stderr, "[ERROR] Command exited with exit code %d\n", exit_status);
                return false;
            }
            break;
        }

        if (posix::WIFSIGNALED(wstatus)) {
            C::fprintf(stderr, "[ERROR] Command process was terminated by %s\n", posix::strsignal(posix::WTERMSIG(wstatus)));
            return false;
        }
    }
    return true;
}

@os(LINUX) func dirExists(path: &char) -> bool {
    let statbuf: Stat = blank;

    if (posix::stat(path, &statbuf) < 0) {
        if (*errno() == ENOENT) return false;
    }
    return true;
}

@os(LINUX) func runCmdSync(cmd: Cmd) -> bool {
    let proc: Handle = runCmdAsync(cmd);
    if (proc.ptr == INVALID_HANDLE.ptr) return false;
    return waitForProcess(proc);
}

@os(LINUX) func runCmdAsync(cmd: Cmd) -> Handle {
    let cpid: i32 = posix::fork();
    if (cpid < 0) {
        C::fprintf(stderr, "[ERROR] Could not fork child process: %s\n", posix::strerror(*errno()));
        return INVALID_HANDLE;
    }

    if (cpid == 0) {
        let str: String = toString(&cmd);
        pushChar(&str, '\0');
        if (cmd.log) {
            C::printf("[CMD] %s\n", str.buffer);
        }

        let progName: String = toString(&cmd.buf.data[0]);
        let args: &&char = asCharPtrs(&cmd);
        if (posix::execvp(progName.buffer as &char, args) < 0) {
            C::fprintf(stderr, "[ERROR] Could not exec child process: %s\n", posix::strerror(*errno()));
            C::exit(1);
        }
        unreachable("execvp");
    }
    return Handle {
        ptr: cpid as usize
    };
}

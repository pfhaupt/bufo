import "prelude.bufo";
import "libc.bufo";
import "substr.bufo";
import "string.bufo";
import "../util/span.bufo";
import "../util/lists.bufo";
import "../util/arena.bufo";
import "../frontend/token.bufo";
import "../frontend/nodes.bufo";
import "../frontend/context.bufo";
import "../frontend/parser.bufo";
import "./checker.bufo";
import "./types.bufo";

struct Lookup {
    files: FileLookupList;
}

func insertFile(this: &Lookup, file: &ParsedFile) -> &FileLookup {
    trace("Lookup.insertFile");
    let lookup: FileLookup = FileLookup {
        globalID: getID(file),
        origin: file.origin,
        imports: blank,
        functions: blank,
        structs: blank,
        variables: blank,
    };
    // Global Scope
    push(&lookup.variables, blank);
    push(&this.files, lookup);
    return last(&this.files);
}

func resolveFunctionByName(this: &Lookup, currentFile: usize, name: &Token) -> FunctionLookupList {
    trace("Lookup.resolveFunctionByName");
    // FIXME: Would be cool if this method didn't create copies of the FunctionLookups
    let currFile: &FileLookup = at(&this.files, currentFile);
    let list: FunctionLookupList = resolveFunctionByName(currFile, name);
    for (let i: usize = 0; i < currFile.imports.length; i = i + 1) {
        let imp: &ImportLookup = at(&currFile.imports, i);
        let importedFile: &FileLookup = at(&this.files, imp.fileID);
        let importList: FunctionLookupList = resolveFunctionByName(importedFile, name);
        extend(&list, &importList);
    }
    return list;
}

func resolveAllFunctionsWithSignature(this: &Lookup, currentFile: usize, name: &Token, typ: &Type) -> FunctionLookupList {
    trace("Lookup.resolveAllFunctionsWithSignature");
    // FIXME: Would be cool if this method didn't create copies of the FunctionLookups
    let currFile: &FileLookup = at(&this.files, currentFile);
    let list: FunctionLookupList = blank;
    extend(&list, &resolveAllFunctionsWithSignature(currFile, name, typ));
    for (let i: usize = 0; i < currFile.imports.length; i = i + 1) {
        let imp: &ImportLookup = at(&currFile.imports, i);
        let importedFile: &FileLookup = at(&this.files, imp.fileID);
        extend(&list, &resolveAllFunctionsWithSignature(importedFile, name, typ));
    }
    return list;
}

func resolveAllStructsWithField(this: &Lookup, currentFile: usize, name: &Token, typ: &Type) -> StructLookupList {
    trace("Lookup.resolveAllStructsWithField");
    // FIXME: Would be cool if this method didn't create copies of the StructLookups
    let currFile = at(&this.files, currentFile);
    let list = resolveAllStructsWithField(currFile, name, typ);
    for (let i: usize = 0; i < currFile.imports.length; i = i + 1) {
        let imp = at(&currFile.imports, i);
        let importedFile = at(&this.files, imp.fileID);
        let importList = resolveAllStructsWithField(importedFile, name, typ);
        extend(&list, &importList);
    }
    return list;
}

func resolveVariableByName(this: &Lookup, currentFile: usize, name: &Token, scopes: &ScopeLookupList, var: &VariableLookup) -> bool {
    trace("Lookup.resolveVariableByName");
    debug(toString(&name.content).buffer);
    // FIXME: The commented loop underflows
    // for (let i: usize = scopes.length - 1; i >= 0; i = i - 1) {
    for (let _i: usize = 0; _i < scopes.length; _i = _i + 1) {
        let i: usize = scopes.length - _i - 1;
        let s: &ScopeLookup = at(scopes, i);
        for (let j: usize = 0; j < s.variables.length; j = j + 1) {
            let v: &VariableLookup = at(&s.variables, j);
            if (equals(&v.name.content, &name.content)) {
                debug("Variable found in current file");
                *var = *v;
                return true;
            }
        }
    }
    let file: &FileLookup = at(&this.files, currentFile);
    let globalScope: &ScopeLookup = at(&file.variables, 0);
    for (let j: usize = 0; j < globalScope.variables.length; j = j + 1) {
        let v: &VariableLookup = at(&globalScope.variables, j);
        if (equals(&v.name.content, &name.content)) {
            debug("Variable found in global scope");
            *var = *v;
            return true;
        }
    }
    for (let i: usize = 0; i < file.imports.length; i = i + 1) {
        let imp: usize = at(&file.imports, i).fileID;
        let file: &FileLookup = at(&this.files, imp);
        let globalScope: &ScopeLookup = at(&file.variables, 0);
        for (let j: usize = 0; j < globalScope.variables.length; j = j + 1) {
            let v: &VariableLookup = at(&globalScope.variables, j);
            if (equals(&v.name.content, &name.content)) {
                debug("Variable found in import");
                *var = *v;
                return true;
            }
        }
    }
    debug("Variable not found");
    return false;
}

func resolveTypeByName(this: &Lookup, currentFile: usize, name: &Token, typ: &TypeLookup) -> bool {
    trace("Lookup.resolveTypeByName");
    let currFile: &FileLookup = at(&this.files, currentFile);
    if (resolveTypeByName(currFile, name, typ)) return true;
    for (let i: usize = 0; i < currFile.imports.length; i = i + 1) {
        let imp: &ImportLookup = at(&currFile.imports, i);
        let importedFile: &FileLookup = at(&this.files, imp.fileID);
        if (resolveTypeByName(importedFile, name, typ)) return true;
    }
    return false;
}

func getTypeLookupForType(this: &Lookup, typ: &Type) -> TypeLookup {
    trace("Lookup.getTypeLookupForType");
    if (isStruct(typ)) {
        let strukt: &ParsedStructDecl = at(&structDecls, typ.typeIndex);
        let fileID: usize = strukt.parentFile;
        let s: &StructLookup = getStructByGlobalID(at(&this.files, fileID), getID(strukt));
        return newTypeLookup(TYPE_LOOKUP_STRUCT, s);
    } else {
        unreachable("Lookup.getTypeLookupForType got non-struct type");
    }
}

func searchAllFilesForTypeName(this: &Lookup, name: &Token) -> TypeLookupList {
    trace("Lookup.searchAllFilesForTypeName");
    let list: TypeLookupList = blank;
    for (let i: usize = 0; i < this.files.length; i = i + 1) {
        let t: TypeLookup = blank;
        if (resolveTypeByName(at(&this.files, i), name, &t)) {
            push(&list, t);
        }
    }
    return list;
}
func searchAllFilesForFunctionName(this: &Lookup, name: &Token) -> FunctionLookupList {
    trace("Lookup.searchAllFilesForFunctionName");
    let list: FunctionLookupList = blank;
    for (let i: usize = 0; i < this.files.length; i = i + 1) {
        let t: FunctionLookupList = resolveFunctionByName(at(&this.files, i), name);
        extend(&list, &t);
    }
    return list;
}
func searchAllFilesForIdentifier(this: &Lookup, name: &Token) -> VariableLookupList {
    trace("Lookup.searchAllFilesForIdentifier");
    let list: VariableLookupList = blank;
    for (let i: usize = 0; i < this.files.length; i = i + 1) {
        let m: &FileLookup = at(&this.files, i);
        for (let j: usize = 0; j < m.variables.length; j = j + 1) {
            let s: &ScopeLookup = at(&m.variables, j);
            let v: VariableLookup = blank;
            if (getVariableByName(s, name.content, &v)) {
                push(&list, v);
            }
        }
    }
    return list;
}

struct FileLookup {
    globalID: usize;
    origin: SubStr;
    imports: ImportLookupList;
    functions: FunctionLookupList;
    structs: StructLookupList;
    variables: ScopeLookupList;
}

func equals(this: &FileLookup, other: &FileLookup) -> bool {
    return equals(&this.origin, &other.origin);
}

func insertTLI(this: &FileLookup, tli: &ParsedTopLevelItem) {
    trace("FileLookup.insertTLI");
    if (tli.kind == TLI_FUNC_DECL) {
        let fun: &ParsedFuncDecl = at(&funcDecls, tli.nodeID);
        let lookup: FunctionLookup = FunctionLookup {
            globalID: tli.nodeID,
            parentFile: this.globalID,
            name: fun.name,
            params: blank,
            fnType: blank,
            isVarArg: fun.params.isVarArg,
        };
        push(&this.functions, lookup);
    } else if (tli.kind == TLI_IMPORT) {
        let fileID: usize = tli.nodeID;
        assert(fileID < files.length);
        let lookup: ImportLookup = ImportLookup {
            fileID: fileID
        };
        if (contains(&this.imports, &lookup)) {
            todo_with_msg("double import");
        }
        push(&this.imports, lookup);
    } else if (tli.kind == TLI_STRUCT_DECL) {
        let strukt: &ParsedStructDecl = at(&structDecls, tli.nodeID);
        let lookup: StructLookup = StructLookup {
            globalID: tli.nodeID,
            parentFile: this.globalID,
            name: strukt.name,
            fields: blank,
            typeID: getID(newType(TYPE_KIND_STRUCT, tli.nodeID)),
        };
        if (contains(&this.structs, &lookup)) {
            todo_with_msg("double struct");
        }
        push(&this.structs, lookup);
    } else if (tli.kind == TLI_VAR_DECL) {
        let varDecl: &ParsedStmt = at(&stmts, tli.nodeID);
        assert(varDecl.kind == STMT_VAR_DECL);
        let lookup: VariableLookup = VariableLookup {
            globalID: tli.nodeID,
            name: varDecl.varDeclContext.name,
            typeState: blank,
            isComptime: varDecl.varDeclContext.isComptime,
        };
        assert(this.variables.length == 1, "expected to find only the global scope");
        let globalScope: &ScopeLookup = last(&this.variables);
        let loc: Token = blank;
        if (isKnownVariable(globalScope, lookup.name.content, &loc)) {
            todo_with_msg("double global var");
        }
        addVariable(globalScope, lookup);
    } else {
        todo_with_msg("insert tli");
    }
}

func containsDuplicateFunction(this: &FileLookup, function: &FunctionLookup, dupl: &&FunctionLookup) -> bool {
    trace("FileLookup.containsDuplicateFunction");
    let name: String = getMangledName(at(&funcDecls, function.globalID));
    let duplicate: bool = false;
    for (let i: usize = 0; !duplicate && i < this.functions.length; i = i + 1) {
        let f: &FunctionLookup = at(&this.functions, i);
        if (f == function) continue;
        let n: String = getMangledName(at(&funcDecls, f.globalID));
        if (equals(&name, &n)) {
            *dupl = f;
            duplicate = true;
        }
        drop(&n);
    }
    return duplicate;
}

func getFunctionByGlobalID(this: &FileLookup, funcID: usize) -> &FunctionLookup {
    trace("FileLookup.getFunctionByGlobalID");
    for (let i: usize = 0; i < this.functions.length; i = i + 1) {
        let f: &FunctionLookup = at(&this.functions, i);
        if (f.globalID == funcID) {
            return f;
        }
    }
    unreachable("Lookup failed to find function based on global ID!");
}

func getStructByGlobalID(this: &FileLookup, structID: usize) -> &StructLookup {
    trace("FileLookup.getStructByGlobalID");
    for (let i: usize = 0; i < this.structs.length; i = i + 1) {
        let s: &StructLookup = at(&this.structs, i);
        if (s.globalID == structID) {
            return s;
        }
    }
    unreachable("Lookup failed to find struct based on global ID!");
}

func resolveFunctionByName(this: &FileLookup, name: &Token) -> FunctionLookupList {
    trace("FileLookup.resolveFunctionByName");
    let list: FunctionLookupList = blank;
    for (let i: usize = 0; i < this.functions.length; i = i + 1) {
        let f: &FunctionLookup = at(&this.functions, i);
        if (equals(&f.name.content, &name.content)) {
            push(&list, *f);
        }
    }
    return list;
}

func resolveAllFunctionsWithSignature(this: &FileLookup, name: &Token, typ: &Type) -> FunctionLookupList {
    trace("FileLookup.resolveAllFunctionsWithSignature");
    let list: FunctionLookupList = blank;
    assert(isFunction(typ), "resolveAllFunctionsWithSignature called with non-function type signature");
    for (let i: usize = 0; i < this.functions.length; i = i + 1) {
        let f: &FunctionLookup = at(&this.functions, i);
        if (!equals(&f.name.content, &name.content)) {
            continue;
        }
        let _rt = at(&types, getType(&f.fnType));
        assert(isFunction(_rt));
        if (typ.fnParams.length != _rt.fnParams.length) continue;
        let retType1 = at(&types, typ.typeIndex);
        let retType2 = at(&types, _rt.typeIndex);
        if (PRINT_DEBUG) {
            let s1 = toString(retType1);
            let s2 = toString(retType2);
            printf("[DEBUG] Return Type 1: %s\n", s1.buffer);
            printf("[DEBUG] Return Type 2: %s\n", s2.buffer);
            drop(&s2);
            drop(&s1);
        }
        if (!isUnknown(retType1)) if (!equals(retType1, retType2)) {
            debug("Not adding: return type mismatch");
            continue;
        }
        let succ = true;
        for (let j: usize = 0; j < typ.fnParams.length; j = j + 1) {
            let p1 = getParam(typ, j);
            let p2 = getParam(_rt, j);
            if (!isType(p1)) continue;
            let t1 = at(&types, getType(p1));
            let t2 = at(&types, getType(p2));
            if (PRINT_DEBUG) {
                let s1 = toString(t1);
                let s2 = toString(t2);
                printf("[DEBUG] Type 1: %s\n", s1.buffer);
                printf("[DEBUG] Type 2: %s\n", s2.buffer);
                drop(&s2);
                drop(&s1);
            }
            if (!isUnknown(t1)) if (!equals(t1, t2)) {
                debug("Not adding function");
                succ = false;
                break;
            }
        }
        if (succ) {
            debug("adding function");
            push(&list, *f);
        }
    }
    return list;
}

func resolveAllStructsWithField(this: &FileLookup, name: &Token, typ: &Type) -> StructLookupList {
    trace("FileLookup.resolveAllStructsWithField");
    let list: StructLookupList = blank;
    for (let i: usize = 0; i < this.structs.length; i = i + 1) {
        let s: &StructLookup = at(&this.structs, i);
        let v: VariableLookup = blank;
        if (resolveFieldByName(s, name, &v)) {
            assert(isType(&v.typeState));
            let t = at(&types, getType(&v.typeState));
            if (equals(t, typ)) {
                push(&list, *s);
            }
        }
    }
    return list;
}

func resolveTypeByName(this: &FileLookup, name: &Token, typ: &TypeLookup) -> bool {
    trace("FileLookup.resolveTypeByName");
    for (let i: usize = 0; i < this.structs.length; i = i + 1) {
        let s: &StructLookup = at(&this.structs, i);
        if (equals(&s.name.content, &name.content)) {
            *typ = newTypeLookup(TYPE_LOOKUP_STRUCT, s);
            return true;
        }
    }
    return false;
}

struct ImportLookup {
    fileID: usize;
}

func equals(this: &ImportLookup, other: &ImportLookup) -> bool {
    return this.fileID == other.fileID;
}

struct FunctionLookup {
    globalID: usize;
    parentFile: usize;
    name: Token;
    fnType: TCState;
    params: VariableLookupList;
    isVarArg: bool;
}

func addParameter(this: &FunctionLookup, globalID: usize, name: Token, typeState: TCState) {
    trace("FunctionLookup.addParameter");
    push(&this.params, VariableLookup {
        globalID: globalID,
        name: name,
        typeState: typeState,
        // REVIEW: Comptime params?
        isComptime: false,
    });
}

func checkForDuplicateParam(this: &FunctionLookup, name: &Token, dupl: &VariableLookup) -> bool {
    trace("FunctionLookup.checkForDuplicateParam");
    for (let i: usize = 0; i < this.params.length; i = i + 1) {
        let param: &VariableLookup = at(&this.params, i);
        if (equals(&param.name.content, &name.content)) {
            *dupl = *param;
            return true;
        }
    }
    return false;
}

func equals(this: &FunctionLookup, other: &FunctionLookup) -> bool {
    warning("When are two functions equal");
    return equals(&this.name, &other.name);
}

comptime TYPE_LOOKUP_INVALID: usize = 0;
comptime TYPE_LOOKUP_STRUCT: usize = 1;
struct TypeLookup {
    kind: usize;
    actual: Any;
}

func getName(this: &TypeLookup) -> String {
    if (this.kind == TYPE_LOOKUP_STRUCT) {
        return toString(&asStruct(this).name.content);
    } else {
        unreachable("Exhaustive handling in TypeLookup.getName");
    }
}

func getLocation(this: &TypeLookup) -> String {
    if (this.kind == TYPE_LOOKUP_STRUCT) {
        return toString(&asStruct(this).name.span);
    } else {
        unreachable("Exhaustive handling in TypeLookup.getLocation");
    }
}

func equals(this: &TypeLookup, other: &TypeLookup) -> bool {
    if (this.kind != other.kind) return false;
    if (this.kind == TYPE_LOOKUP_STRUCT) {
        return equals(asStruct(this), asStruct(other));
    }
    unreachable("Exhaustive handling in TypeLookup.equals");
}

func isStruct(this: &TypeLookup) -> bool {
    return this.kind == TYPE_LOOKUP_STRUCT;
}

func asStruct(this: &TypeLookup) -> &StructLookup {
    assert(isStruct(this), "Attempted to convert non-struct TypeLookup into StructLookup");
    return this.actual as &StructLookup;
}

func newTypeLookup(kind: usize, actual: Any) -> TypeLookup {
    return TypeLookup {
        kind: kind,
        actual: actual
    };
}

struct StructLookup {
    globalID: usize;
    parentFile: usize;
    name: Token;
    fields: VariableLookupList;
    typeID: usize;
}

func asTypeLookup(this: &StructLookup) -> TypeLookup {
    return newTypeLookup(TYPE_LOOKUP_STRUCT, this);
}
func equals(this: &StructLookup, other: &StructLookup) -> bool {
    if (this.parentFile != other.parentFile) return false;
    return equals(&this.name.content, &other.name.content);
}

func resolveFieldByName(this: &StructLookup, name: &Token, out: &VariableLookup) -> bool {
    trace("StructLookup.resolveFieldByName");
    for (let i: usize = 0; i < this.fields.length; i = i + 1) {
        let f: &VariableLookup = at(&this.fields, i);
        if (equals(&f.name.content, &name.content)) {
            *out = *f;
            return true;
        }
    }
    return false;
}

func hasField(this: &StructLookup, name: &SubStr) -> bool {
    trace("StructLookup.hasField");
    for (let i: usize = 0; i < this.fields.length; i = i + 1) {
        let f: &VariableLookup = at(&this.fields, i);
        if (equals(&f.name.content, name)) {
            return true;
        }
    }
    return false;
}

func getIndexByName(this: &StructLookup, name: &Token) -> usize {
    trace("StructLookup.getIndexByName");
    for (let i: usize = 0; i < this.fields.length; i = i + 1) {
        let f: &VariableLookup = at(&this.fields, i);
        if (equals(&f.name.content, &name.content)) {
            return i;
        }
    }
    assert(hasField(this, &name.content), "StructLookup.getIndexByName() called when StructLookup.hasField() == false");
    unreachable("StructLookup.getIndexByName does not have a field with the given name");
}

func checkForDuplicateField(this: &StructLookup, name: &Token, dupl: &VariableLookup) -> bool {
    trace("StructLookup.checkForDuplicateField");
    for (let i: usize = 0; i < this.fields.length; i = i + 1) {
        let f: &VariableLookup = at(&this.fields, i);
        if (equals(&f.name.content, &name.content)) {
            *dupl = *f;
            return true;
        }
    }
    return false;
}

func addField(this: &StructLookup, globalID: usize, name: Token, typeState: TCState) {
    trace("StructLookup.addField");
    push(&this.fields, VariableLookup {
        globalID: globalID,
        name: name,
        typeState: typeState,
        // REVIEW: Comptime fields?
        isComptime: false,
    });
}

struct VariableLookup {
    globalID: usize;
    name: Token;
    typeState: TCState;
    isComptime: bool;
}

func isParameter(this: &VariableLookup) -> bool {
    return this.globalID >= NODE_ID_OFFSET;
}

func equals(this: &VariableLookup, other: &VariableLookup) -> bool {
    return equals(&this.name.content, &other.name.content);
}

struct ScopeLookup {
    variables: VariableLookupList;
}

func equals(this: &ScopeLookup, other: &ScopeLookup) -> bool {
    todo_with_msg("when are two scopes equal");
}
func addVariable(this: &ScopeLookup, variable: VariableLookup) {
    trace("ScopeLookup.addVariable");
    push(&this.variables, variable);
}

func isKnownVariable(this: &ScopeLookup, name: SubStr, existing: &Token) -> bool {
    trace("ScopeLookup.isKnownVariable");
    for (let i: usize = 0; i < this.variables.length; i = i + 1) {
        let v: &VariableLookup = at(&this.variables, i);
        if (equals(&v.name.content, &name)) {
            *existing = v.name;
            return true;
        }
    }
    return false;
}

func getVarIndexByName(this: &ScopeLookup, name: SubStr, index: &usize) -> bool {
    trace("ScopeLookup.getVarIndexByName");
    for (let i: usize = 0; i < this.variables.length; i = i + 1) {
        let v: &VariableLookup = at(&this.variables, i);
        if (equals(&v.name.content, &name)) {
            *index = i;
            return true;
        }
    }
    return false;
}

func getVariableByName(this: &ScopeLookup, name: SubStr, var: &VariableLookup) -> bool {
    trace("ScopeLookup.getVariableByName");
    for (let i: usize = 0; i < this.variables.length; i = i + 1) {
        let v: &VariableLookup = at(&this.variables, i);
        if (equals(&v.name.content, &name)) {
            *var = *v;
            return true;
        }
    }
    return false;
}

func getVariableAtIndex(this: &ScopeLookup, index: usize) -> &VariableLookup {
    trace("ScopeLookup.getVariableAtIndex");
    return at(&this.variables, index);
}

func clear(this: &ScopeLookup) {
    clear(&this.variables);
}


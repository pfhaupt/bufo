import "prelude.bufo";
import "string.bufo";
import "substr.bufo";
import "../../bufo.bufo";
import "../LLVM/bindings.bufo";
import "../LLVM/module.bufo";
import "../LLVM/builder.bufo";
import "../LLVM/types.bufo";
import "../LLVM/values.bufo";
import "../LLVM/basic_block.bufo";
import "../LLVM/target.bufo";
import "../LLVM/attribute.bufo";

struct LLVMContext {
    ref: LLVM::ContextRef;
}

func drop(this: &LLVMContext) {
    LLVM::ContextDispose(this.ref);
}

func guardReference(this: &LLVMContext) {
    assert(!LLVM::isNull(this.ref), "LLVM Context is null");
}

func initializeTargets(this: &LLVMContext) {
    assert(LLVM::_InitializeNativeAsmParser() == 0, "Could not initialize Targets");
    assert(LLVM::_InitializeNativeAsmPrinter() == 0, "Could not initialize Targets");
    assert(LLVM::_InitializeNativeDisassembler() == 0, "Could not initialize Targets");
    assert(LLVM::_InitializeNativeTarget() == 0, "Could not initialize Targets");
}

func createTarget(this: &LLVMContext, name: &char) -> LLVMTarget {
    let target: LLVM::TargetRef = LLVM::GetTargetFromName(name);
    assert(!LLVM::isNull(target), "Could not create target triple");
    return newLLVMTargetFromRef(target);
}

func createTargetTriple(this: &LLVMContext, name: &char) -> &char {
    if (equals(flags.target, &newStringFromStrLit("WINDOWS"))) {
        return "x86_64-pc-windows-msvc";
    } else if (equals(flags.target, &newStringFromStrLit("LINUX"))) {
        return "x86_64-unknown-linux-gnu";
    } else {
        unreachable("createTargetTriple: Unknown target!");
    }
}

func createModuleWithName(this: &LLVMContext, name: SubStr) -> LLVMModule {
    guardReference(this);
    let _name: String = toString(&name);
    let mod: LLVM::ModuleRef = LLVM::ModuleCreateWithNameInContext(_name.buffer, this.ref);
    drop(&_name);
    assert(!LLVM::isNull(mod), "Could not create module in context");
    return newLLVMModuleFromRef(mod);
}

func createBuilder(this: &LLVMContext) -> LLVMBuilder {
    guardReference(this);
    let builder: LLVM::BuilderRef = LLVM::CreateBuilderInContext(this.ref);
    assert(!LLVM::isNull(builder), "Could not create builder in context");
    return newLLVMBuilderFromRef(builder);
}

func createIntegerType(this: &LLVMContext, bitsize: i64) -> LLVMType {
    assert(bitsize > 0, "Got invalid bitsize in createIntegerType");
    // LLVM max limit is 2^23-1 bits
    assert(bitsize <= 8388607, "Got invalid bitsize in createIntegerType");
    let int: LLVM::TypeRef = LLVM::IntTypeInContext(this.ref, bitsize as u32);
    assert(!LLVM::isNull(int), "Could not create integer type in context");
    return newLLVMTypeFromRef(int);
}

func createFloatType(this: &LLVMContext) -> LLVMType {
    let float: LLVM::TypeRef = LLVM::FloatTypeInContext(this.ref);
    assert(!LLVM::isNull(float), "Could not create float type in context");
    return newLLVMTypeFromRef(float);
}

func createDoubleType(this: &LLVMContext) -> LLVMType {
    let double: LLVM::TypeRef = LLVM::DoubleTypeInContext(this.ref);
    assert(!LLVM::isNull(double), "Could not create double type in context");
    return newLLVMTypeFromRef(double);
}

func createStructType(this: &LLVMContext, elements: &LLVMType, count: u32, packed: bool) -> LLVMType {
    let b: LLVMBool = newLLVMBool(packed);
    let strukt: LLVM::TypeRef = LLVM::StructTypeInContext(this.ref, elements as &LLVM::TypeRef, count, b);
    assert(!LLVM::isNull(strukt), "Could not create struct type in context");
    return newLLVMTypeFromRef(strukt);
}

func createVoidType(this: &LLVMContext) -> LLVMType {
    let void: LLVM::TypeRef = LLVM::VoidTypeInContext(this.ref);
    assert(!LLVM::isNull(void), "Could not create void type in context");
    return newLLVMTypeFromRef(void);
}

func appendBasicBlock(this: &LLVMContext, fn: LLVMValue, name: SubStr) -> LLVMBasicBlock {
    let _name: String = toString(&name);
    let block: LLVM::BasicBlockRef = LLVM::AppendBasicBlockInContext(this.ref, fn.ref, _name.buffer);
    drop(&_name);
    assert(!LLVM::isNull(block), "Could not append basic block in context");
    return newLLVMBasicBlockFromRef(block);
}

func createEnumAttribute(this: &LLVMContext, id: u32, val: u64) -> LLVMAttribute {
    let attr = LLVM::CreateEnumAttribute(this.ref, id, val);
    assert(!LLVM::isNull(attr), "Could not create enum attribute in context");
    return newLLVMAttributeFromRef(attr);
}

func newLLVMContext() -> LLVMContext {
    let ref: LLVM::ContextRef = LLVM::ContextCreate();
    assert(!LLVM::isNull(ref), "Could not create LLVM Context");
    return LLVMContext {
        ref: ref,
    };
}

import "prelude.bufo";
import "substr.bufo";
import "./bindings.bufo";
import "./basic_block.bufo";
import "../LLVM/types.bufo";
import "../LLVM/attribute.bufo";

struct LLVMValue {
    ref: LLVM::ValueRef;
}
func equals(this: &LLVMValue, other: &LLVMValue) -> bool {
    todo_with_msg("LLVMValue.equals");
}
func guardReference(this: &LLVMValue) {
    assert(!LLVM::isNull(this.ref), "LLVM Value is null");
}

func isStructValue(this: &LLVMValue) -> bool {
    let t: LLVMType = getType(this);
    return isStruct(&t);
}

func dump(this: &LLVMValue) {
    LLVM::DumpValue(this.ref);
}

func getGlobalType(this: &LLVMValue) -> LLVMType {
    let t: LLVM::TypeRef = LLVM::GlobalGetValueType(this.ref);
    assert(!LLVM::isNull(t), "Could not get global type of LLVMValue");
    return newLLVMTypeFromRef(t);
}

func getNthParam(this: &LLVMValue, index: usize) -> LLVMValue {
    let param: LLVM::ValueRef = LLVM::GetParam(this.ref, index as u32);
    assert(!LLVM::isNull(param), "Could not get parameter of LLVMValue");
    return newLLVMValueFromRef(param);
}

func getFirstBasicBlock(this: &LLVMValue) -> LLVMBasicBlock {
    let block: LLVM::BasicBlockRef = LLVM::GetFirstBasicBlock(this.ref);
    assert(!LLVM::isNull(block), "Could not get first basic block from LLVMValue");
    return newLLVMBasicBlockFromRef(block);
}

func getType(this: &LLVMValue) -> LLVMType {
    let t: LLVM::TypeRef = LLVM::TypeOf(this.ref);
    assert(!LLVM::isNull(t), "Could not get type of LLVMValue");
    return newLLVMTypeFromRef(t);
}
func setName(this: &LLVMValue, name: SubStr) {
    LLVM::SetValueName2(this.ref, name.start, name.len);
}

func getName(this: &LLVMValue) -> SubStr {
    let len: usize = 0;
    let name: &char = LLVM::GetValueName2(this.ref, &len);
    return newSubStr(name, 0, len);
}

func setInitializer(this: &LLVMValue, val: LLVMValue) {
    assert(!LLVM::isNull(val.ref), "Expected valid LLVMValue in LLVMValue.setInitializer");
    LLVM::SetInitializer(this.ref, val.ref);
}
func getInitializer(this: &LLVMValue) -> LLVMValue {
    let val = LLVM::GetInitializer(this.ref);
    assert(!LLVM::isNull(val), "Could not get initializer of LLVMValue");
    return newLLVMValueFromRef(val);
}

func addAttribute(this: &LLVMValue, index: u32, attr: LLVMAttribute) {
    LLVM::AddAttributeAtIndex(this.ref, index, attr.ref);
}

func newLLVMValueFromRef(ref: LLVM::ValueRef) -> LLVMValue {
    assert(!LLVM::isNull(ref), "Passed invalid reference to newLLVMValueFromRef");
    return LLVMValue {
        ref: ref
    };
}

import "prelude.bufo";
import "libc.bufo";
import "substr.bufo";
import "./token.bufo";
import "./nodes.bufo";
import "./lexer.bufo";
import "../util/arena.bufo";
import "../util/lists.bufo";
import "../middleend/types.bufo";
import "../middleend/checker.bufo";

// FIXME: This name is misleading, given that we also use it for function calls
struct ArrayContext {
    elemList: &usize;
    elemLength: usize;
    elemCapacity: usize;
    size: usize;
}

func addElement(this: &ArrayContext, elem: &ParsedExpr) {
    if (this.elemLength >= this.elemCapacity) {
        let newCap: usize = this.elemCapacity * 2;
        if (newCap == 0) newCap = 1;
        this.elemList = C::realloc(this.elemList, newCap * sizeof usize);
        assert(this.elemList != null, "Could not resize elem list");
        this.elemCapacity = newCap;
    }
    this.elemList[this.elemLength] = getID(elem);
    this.elemLength = this.elemLength + 1;
}

func getElementAtIndex(this: &ArrayContext, index: usize) -> usize {
    assert(index < this.elemLength, "Out of bounds access in ArrayContext.getElementAtIndex");
    return this.elemList[index];
}

struct StructContext {
    fieldNames: &Token;
    fieldTypes: &&ParsedTypeNode;
    fieldLength: usize;
    fieldCapacity: usize;
}
func addField(this: &StructContext, name: Token, typ: &ParsedTypeNode) {
    if (this.fieldLength >= this.fieldCapacity) {
        let newCap: usize = this.fieldCapacity * 2;
        if (newCap == 0) newCap = 1;
        this.fieldNames = C::realloc(this.fieldNames, newCap * sizeof Token);
        this.fieldTypes = C::realloc(this.fieldTypes, newCap * sizeof usize);
        assert(this.fieldNames != null, "Could not resize field list");
        assert(this.fieldTypes != null, "Could not resize field list");
        this.fieldCapacity = newCap;
    }
    this.fieldNames[this.fieldLength] = name;
    this.fieldTypes[this.fieldLength] = typ;
    this.fieldLength = this.fieldLength + 1;
}
func getFieldNameAtIndex(this: &StructContext, index: usize) -> Token {
    assert(index < this.fieldLength, "Out of bounds access in StructContext.getFieldNameAtIndex");
    return this.fieldNames[index];
}
func getFieldTypeAtIndex(this: &StructContext, index: usize) -> &ParsedTypeNode {
    assert(index < this.fieldLength, "Out of bounds access in StructContext.getFieldTypeAtIndex");
    return this.fieldTypes[index];
}
func getFieldIndex(this: &StructContext, name: &SubStr) -> usize {
    for (let i: usize = 0; i < this.fieldLength; i = i + 1) {
        let f: Token = getFieldNameAtIndex(this, i);
        if (equals(&f.content, name)) {
            return i;
        }
    }
    unreachable("Expected to find field index at this point");
}
func getFieldOffset(this: &StructContext, name: &SubStr) -> usize {
    let offset: usize = 0;
    for (let i: usize = 0; i < this.fieldLength; i = i + 1) {
        let f: Token = getFieldNameAtIndex(this, i);
        let _t: &ParsedTypeNode = getFieldTypeAtIndex(this, i);
        assert(isSuccess(&_t.typeState), "Expected valid field type in getFieldOffset");
        let t: &Type = getType(&_t.typeState);
        if (equals(&f.content, name)) {
            return offset;
        }
        offset = offset + getSize(t);
    }
    unreachable("Expected to find field offset at this point");
}

struct StructInitContext {
    fieldNames: &Token;
    fieldExprs: &&ParsedExpr;
    fieldLength: usize;
    fieldCapacity: usize;
}
func addField(this: &StructInitContext, name: Token, expr: &ParsedExpr) {
    if (this.fieldLength >= this.fieldCapacity) {
        let newCap: usize = this.fieldCapacity * 2;
        if (newCap == 0) newCap = 1;
        this.fieldNames = C::realloc(this.fieldNames, newCap * sizeof Token);
        this.fieldExprs = C::realloc(this.fieldExprs, newCap * sizeof usize);
        assert(this.fieldNames != null, "Could not resize field list");
        assert(this.fieldExprs != null, "Could not resize field list");
        this.fieldCapacity = newCap;
    }
    this.fieldNames[this.fieldLength] = name;
    this.fieldExprs[this.fieldLength] = expr;
    this.fieldLength = this.fieldLength + 1;
}
func getFieldNameAtIndex(this: &StructInitContext, index: usize) -> Token {
    assert(index < this.fieldLength, "Out of bounds access in StructInitContext.getFieldNameAtIndex");
    return this.fieldNames[index];
}
func getFieldExprAtIndex(this: &StructInitContext, index: usize) -> &ParsedExpr {
    assert(index < this.fieldLength, "Out of bounds access in StructInitContext.getFieldExprAtIndex");
    return this.fieldExprs[index];
}

struct ParamContext {
    paramNames: &Token;
    paramTypes: &&ParsedTypeNode;
    paramLength: usize;
    paramCapacity: usize;
    isVarArg: bool;
}
func addParameter(this: &ParamContext, name: Token, typ: &ParsedTypeNode) {
    if (this.paramLength >= this.paramCapacity) {
        let newCap: usize = this.paramCapacity * 2;
        if (newCap == 0) newCap = 1;
        this.paramNames = C::realloc(this.paramNames, newCap * sizeof Token);
        this.paramTypes = C::realloc(this.paramTypes, newCap * sizeof usize);
        assert(this.paramNames != null, "Could not resize param list");
        assert(this.paramTypes != null, "Could not resize param list");
        this.paramCapacity = newCap;
    }
    this.paramNames[this.paramLength] = name;
    this.paramTypes[this.paramLength] = typ;
    this.paramLength = this.paramLength + 1;
}

func hasThis(this: &ParamContext) -> bool {
    if (this.paramLength == 0) return false;
    return equals(&getNameAtIndex(this, 0).content, &comptime BUILD_A_KEYWORD("this"));
}

func getNameAtIndex(this: &ParamContext, index: usize) -> Token {
    assert(index < this.paramLength, "Out of bounds access in ParamContext.getNameAtIndex");
    return this.paramNames[index];
}
func getTypeAtIndex(this: &ParamContext, index: usize) -> &ParsedTypeNode {
    assert(index < this.paramLength, "Out of bounds access in ParamContext.getTypeAtIndex");
    return this.paramTypes[index];
}

comptime ELSE_BIT: usize = pow2(20);
// condition is implicitly stored in ParsedStmt.expr
struct IfContext {
    thenBody: &ParsedStmt;
    elseBody: &ParsedStmt;
    hasElse: bool;
}


import "prelude.bufo";
import "libc.bufo";
import "substr.bufo";
import "./token.bufo";
import "./nodes.bufo";
import "./lexer.bufo";
import "../middleend/types.bufo";

struct VarDeclContext {
    name: Token;
    typeID: usize;
    exprID: usize;
    isComptime: bool;
}

// FIXME: This name is misleading, given that we also use it for function calls
struct ArrayContext {
    elemList: &usize;
    elemLength: usize;
    elemCapacity: usize;
    size: usize;

    func addElement(&this, elem: &ParsedExpr) {
        trace("ArrayContext.addElement");
        if (this.elemLength >= this.elemCapacity) {
            let newCap: usize = this.elemCapacity * 2;
            if (newCap == 0) newCap = 1;
            this.elemList = realloc(this.elemList, newCap * sizeof usize);
            assert_with_msg(this.elemList != null, "Could not resize elem list");
            this.elemCapacity = newCap;
        }
        *(this.elemList + this.elemLength * sizeof usize) = elem.getID();
        this.elemLength = this.elemLength + 1;
    }

    func getElementAtIndex(&this, index: usize) -> usize {
        assert_with_msg(index < this.elemLength, "Out of bounds access in ArrayContext.getElementAtIndex");
        return *(this.elemList + index * sizeof usize);
    }
}

struct StructContext {
    fieldNames: &Token;
    fieldTypes: &usize;
    fieldLength: usize;
    fieldCapacity: usize;
    methods: &usize;
    methodLength: usize;
    methodCapacity: usize;
    func addField(&this, name: Token, typ: &ParsedTypeNode) {
        trace("StructContext.addField");
        if (this.fieldLength >= this.fieldCapacity) {
            let newCap: usize = this.fieldCapacity * 2;
            if (newCap == 0) newCap = 1;
            this.fieldNames = realloc(this.fieldNames, newCap * sizeof Token);
            this.fieldTypes = realloc(this.fieldTypes, newCap * sizeof usize);
            assert_with_msg(this.fieldNames != null, "Could not resize field list");
            assert_with_msg(this.fieldTypes != null, "Could not resize field list");
            this.fieldCapacity = newCap;
        }
        *(this.fieldNames + this.fieldLength * sizeof Token) = name;
        *(this.fieldTypes + this.fieldLength * sizeof usize) = typ.getID();
        this.fieldLength = this.fieldLength + 1;
    }
    func addMethod(&this, method: &ParsedMethod) {
        trace("StructContext.addMethod");
        if (this.methodLength >= this.methodCapacity) {
            let newCap: usize = this.methodCapacity * 2;
            if (newCap == 0) newCap = 1;
            this.methods = realloc(this.methods, newCap * sizeof usize);
            assert_with_msg(this.methods != null, "Could not resize method list");
            this.methodCapacity = newCap;
        }
        *(this.methods + this.methodLength * sizeof usize) = method.getID();
        this.methodLength = this.methodLength + 1;
    }
    func getFieldNameAtIndex(&this, index: usize) -> Token {
        trace("StructContext.getFieldNameAtIndex");
        assert_with_msg(index < this.fieldLength, "Out of bounds access in StructContext.getFieldNameAtIndex");
        return *(this.fieldNames + index * sizeof Token);
    }
    func getFieldTypeAtIndex(&this, index: usize) -> usize {
        trace("StructContext.getFieldTypeAtIndex");
        assert_with_msg(index < this.fieldLength, "Out of bounds access in StructContext.getFieldTypeAtIndex");
        return *(this.fieldTypes + index * sizeof usize);
    }
    func getMethodAtIndex(&this, index: usize) -> usize {
        trace("StructContext.getMethodAtIndex");
        assert_with_msg(index < this.methodLength, "Out of bounds access in StructContext.getMethodAtIndex");
        return *(this.methods + index * sizeof usize);
    }
    func getFieldIndex(&this, name: &SubStr) -> usize {
        trace("StructContext.getFieldIndex");
        for (let i: usize = 0; i < this.fieldLength; i = i + 1) {
            let f: Token = this.getFieldNameAtIndex(i);
            if (f.content.equals(name)) {
                return i;
            }
        }
        unreachable("Expected to find field index at this point");
        return 0;
    }
    func getFieldOffset(&this, name: &SubStr) -> usize {
        trace("StructContext.getFieldOffset");
        let offset: usize = 0;
        for (let i: usize = 0; i < this.fieldLength; i = i + 1) {
            let f: Token = this.getFieldNameAtIndex(i);
            let _t: &ParsedTypeNode = typeNodes.at(this.getFieldTypeAtIndex(i));
            assert_with_msg(_t.typeState.isSuccess(), "Expected valid field type in getFieldOffset");
            let t: &Type = types.at(_t.typeState.getType());
            if (f.content.equals(name)) {
                return offset;
            }
            offset = offset + t.getSize();
        }
        unreachable("Expected to find field offset at this point");
        return 0;
    }
}

struct StructInitContext {
    fieldNames: &Token;
    fieldExprs: &usize;
    fieldLength: usize;
    fieldCapacity: usize;
    func addField(&this, name: Token, expr: &ParsedExpr) {
        trace("StructInitContext.addField");
        if (this.fieldLength >= this.fieldCapacity) {
            let newCap: usize = this.fieldCapacity * 2;
            if (newCap == 0) newCap = 1;
            this.fieldNames = realloc(this.fieldNames, newCap * sizeof Token);
            this.fieldExprs = realloc(this.fieldExprs, newCap * sizeof usize);
            assert_with_msg(this.fieldNames != null, "Could not resize field list");
            assert_with_msg(this.fieldExprs != null, "Could not resize field list");
            this.fieldCapacity = newCap;
        }
        *(this.fieldNames + this.fieldLength * sizeof Token) = name;
        *(this.fieldExprs + this.fieldLength * sizeof usize) = expr.getID();
        this.fieldLength = this.fieldLength + 1;
    }
    func getFieldNameAtIndex(&this, index: usize) -> Token {
        trace("StructInitContext.getFieldNameAtIndex");
        assert_with_msg(index < this.fieldLength, "Out of bounds access in StructInitContext.getFieldNameAtIndex");
        return *(this.fieldNames + index * sizeof Token);
    }
    func getFieldExprAtIndex(&this, index: usize) -> usize {
        trace("StructInitContext.getFieldExprAtIndex");
        assert_with_msg(index < this.fieldLength, "Out of bounds access in StructInitContext.getFieldExprAtIndex");
        return *(this.fieldExprs + index * sizeof usize);
    }
}

struct ParamContext {
    paramNames: &Token;
    paramTypes: &usize;
    paramLength: usize;
    paramCapacity: usize;
    isVarArg: bool;
    func addParameter(&this, name: Token, typ: &ParsedTypeNode) {
        trace("ParamContext.addParameter");
        if (this.paramLength >= this.paramCapacity) {
            let newCap: usize = this.paramCapacity * 2;
            if (newCap == 0) newCap = 1;
            this.paramNames = realloc(this.paramNames, newCap * sizeof Token);
            this.paramTypes = realloc(this.paramTypes, newCap * sizeof usize);
            assert_with_msg(this.paramNames != null, "Could not resize param list");
            assert_with_msg(this.paramTypes != null, "Could not resize param list");
            this.paramCapacity = newCap;
        }
        *(this.paramNames + this.paramLength * sizeof Token) = name;
        *(this.paramTypes + this.paramLength * sizeof usize) = typ.getID();
        this.paramLength = this.paramLength + 1;
    }

    func hasThis(&this) -> bool {
        if (this.paramLength == 0) return false;
        return this.getNameAtIndex(0).content.equals(&KEYWORD_THIS);
    }

    func getNameAtIndex(&this, index: usize) -> Token {
        trace("ParamContext.getNameAtIndex");
        assert_with_msg(index < this.paramLength, "Out of bounds access in ParamContext.getNameAtIndex");
        return *(this.paramNames + index * sizeof Token);
    }
    func getTypeAtIndex(&this, index: usize) -> usize {
        trace("ParamContext.getTypeAtIndex");
        assert_with_msg(index < this.paramLength, "Out of bounds access in ParamContext.getTypeAtIndex");
        return *(this.paramTypes + index * sizeof usize);
    }
}

comptime ELSE_BIT: usize = pow2(20);
// condition is implicitly stored in ParsedStmt.expr
struct IfContext {
    thenBody: usize;
    elseBody: usize;
    hasElse: bool;
}

struct ForContext {
    init: usize;
    cond: usize;
    step: usize;
    body: usize;
}

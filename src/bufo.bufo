import "prelude.bufo";
import "libc.bufo";
import "substr.bufo";
import "string.bufo";
import "cmd.bufo";
import "./util/span.bufo";
import "./frontend/nodes.bufo";
import "./frontend/lexer.bufo";
import "./frontend/parser.bufo";
import "./middleend/types.bufo";
import "./middleend/checker.bufo";
import "./backend/irgen.bufo";
import "./backend/interp.bufo";
import "./backend/codegen_llvm.bufo";

func getArg(argv: &&char, i: usize) -> &char {
    return *(argv + i * sizeof &char);
}

func main(argc: i32, argv: &&char) -> i32 {
    for (let i: usize = 0; i < PARSED_TYPE_BUILTIN; i = i + 1) {
        typeNodes.push(ParsedTypeNode {
            span: blank,
            kind: PARSED_TYPE_BUILTIN,
        });
    }
    for (let i: usize = 0; i < TYPE_COUNT; i = i + 1) {
        types.push(blank);
    }
    setupStdHandles();
    if (argc < 3) {
        fprintf(stderr, "error: Expected input and output files.\n");
        exit(1);
    }
    // TODO: Handle other arguments such as -v and -o
    // TODO: Handle arguments in any order
    let _prog: &char = getArg(argv, 0);
    let file: &char = getArg(argv, 1);
    let out: &char = getArg(argv, 2);
    let filepath: String = newStringFromStrLit(file);
    filepath = filepath.toAbsolutePath();
    let fileSubStr: SubStr = filepath.asSubStr();
    let string: String = readFileToString(file);
    let sub: SubStr = newSubStrOfString(&string, 0, string.length);
    let parser: Parser = newParserFromSource(0, sub);
    parser.addImportRoot(newSubStrOfStrLit("./std/"));
    let project: ParsedModule = parser.parseProject(fileSubStr);
    let checker: TypeChecker = newTypeChecker();
    if (!checker.typeCheckProject(&project)) return 1;
    let generator: IRGen = newIRGenerator();
    if (!generator.generateBytecode(&project)) return 1;
    let interp: IRInterp = newIRInterpreter(&generator);
    let llvmCodegen: LLVMCodegen = newLLVMCodegen(fileSubStr, &generator, &interp);
    if (!llvmCodegen.generateExecutable(newSubStrOfStrLit(out))) return 1;
    if (PRINT_DEBUG) {
        printf("modules.length       = %llu\n", modules.length);
        printf("topLevelItems.length = %llu\n", topLevelItems.length);
        printf("structDecls.length   = %llu\n", structDecls.length);
        printf("funcDecls.length     = %llu\n", funcDecls.length);
        printf("methods.length       = %llu\n", methods.length);
        printf("blocks.length        = %llu\n", blocks.length);
        printf("stmts.length         = %llu\n", stmts.length);
        printf("typeNodes.length     = %llu\n", typeNodes.length);
        printf("exprs.length         = %llu\n", exprs.length);
    }
    let s: String = newStringFromStrLit("link.exe ");
    s.pushStr(out);
    s.pushStr(".obj");
    s.pushStr(" /DEFAULTLIB:libcmt /OUT:");
    s.pushStr(out);
    for (let i: usize = 0; i < compilerFlags.length; i = i + 1) {
        s.pushChar(' ');
        let flag: &ParsedCompilerFlag = &*compilerFlags.at(i);
        if (flag.flag.content.equals(&COMPILER_FLAG_LIBRARY)) {
            s.pushSubStr(&flag.value.content);
            s.pushStr(".lib");
        } else if (flag.flag.content.equals(&COMPILER_FLAG_LIBPATH)) {
            s.pushStr("/LIBPATH:");
            s.pushSubStr(&flag.value.content);
        } else if (flag.flag.content.equals(&COMPILER_FLAG_LINKER)) {
            s.pushSubStr(&flag.value.content);
        } else {
            unreachable("Exhaustive handling of compiler_flags");
        }
    }
    let cmd: Cmd = newCmdFromStrLit(s.chars());
    if (!cmd.runSync()) return 1;
    printf("%s: Successfully compiled code and saved it in %s :^)\n", NOTE_STR, out);
    string.drop();
    return 0;
}

import "prelude.bufo";
import "string.bufo";
import "../frontend/nodes.bufo";
import "./arena.bufo";

struct Span {
    mod: usize;
    start: usize;
    end: usize;
}
func equals(this: &Span, other: &Span) -> bool {
    return this.mod == other.mod && this.start == other.start && this.end == other.end;
}
func print(this: &Span) {
    let s: String = toString(this);
    s.print();
    s.drop();
}
func toString(this: &Span) -> String {
    // FIXME: The spans report in bytes, but the formatting makes it look like line and column
    let s: String = at(&modules, this.mod).origin.toString();
    s.pushChar(':');
    s.pushNumber(this.start);
    s.pushChar(':');
    s.pushNumber(this.end);
    return s;
}
func comesBefore(this: &Span, other: &Span) -> bool {
    return this.start < other.start;
}

func newSpan(mod: usize, start: usize, end: usize) -> Span {
    assert(end >= start, "Invalid size for newSpan()");
    return Span {
        mod: mod,
        start: start,
        end: end,
    };
}
func newSpanBetween(start: &Span, end: &Span) -> Span {
    // (100:105)+(104:109)
    assert(end.end >= start.end, "newSpanBetween: End Span can't end before Start Span.");
    assert(start.start <= end.start, "newSpanBetween: Start Span can't start after End Span.");
    assert(start.mod == end.mod, "newSpanBetween: Spans can't be in different modules.");
    return Span {
        mod: start.mod,
        start: start.start,
        end: end.end
    };
}
func defaultSpan() -> Span {
    return Span {
        mod: 0,
        start: 0,
        end: 0
    };
}
